plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'maven-publish'
}

group 'me.zeroeightsix'
version '1.0.0'

def ENV = System.getenv()
version = version + "-" + (ENV.BUILD_NUMBER ? ENV.BUILD_NUMBER : "local")

repositories {
    mavenCentral()
    maven {
        url = "https://maven.modmuss50.me/"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "me.zeroeightsix:fiber:0.12.1-SNAPSHOT"
    testImplementation "me.zeroeightsix:fiber:0.12.1-SNAPSHOT"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact (jar)
        }
    }

    repositories {
        if (project.hasProperty('zeroeightsix_pass')) {
            maven {
                url = "http://deploy.modmuss50.me/"
                credentials {
                    username = "zeroeightsix"
                    password = project.getProperty('zeroeightsix_pass')
                }
            }
        }
    }
}